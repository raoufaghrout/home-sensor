apply plugin: 'java'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

sourceSets {
    testIntegration {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/testIntegration/java')
        }

        resources.srcDir file('src/testIntegration/resources')
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    testIntegrationCompile.extendsFrom testCompile
    testIntegrationRuntime.extendsFrom testRuntime
}

idea {
    module {
        testSourceDirs += file('src/testIntegration/java')
    }
}

dependencies {
    compile (
        'com.amazonaws:aws-lambda-java-core:1.1.0',
        'com.amazonaws:aws-lambda-java-log4j:1.0.0',
        'com.fasterxml.jackson.core:jackson-core:2.8.5',
        'com.fasterxml.jackson.core:jackson-databind:2.8.5',
        'com.fasterxml.jackson.core:jackson-annotations:2.8.5'
    )

    compile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.145'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.9'
}

task buildZip(type: Zip) {
    baseName = "home-sensor"
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

task testIntegration(type: Test) {
    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
}

build.dependsOn testIntegration
build.dependsOn buildZip

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
